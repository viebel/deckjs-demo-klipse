= Polymorphism à la Clojure: "Functional Object Oriented Programming" is not an Oxymoron
:encoding: utf-8
:navigation:
:deckjs_transition: horizontal-slide
:menu:
:status:
:deckjs_theme: none
:hardbreaks:

++++
<pre class="hidden"><code class="clj">
(ns my.playground)
</code></pre>
++++


== Intro

++++
<section class="slide">
        <div class="text-centered">
            <div class="centered" style="font-size:1.4em;">
            <div style="font-size:2em;"> Polymorphism à la Clojure:
    <br>
    <span style="font-style:italic;m">Functional Object Oriented Programming</span>
    <br>
    is not an Oxymoron
    </div>
    <br>
    <br>
              <div class="left" style="width:50%;">
	      <div> Interactive Slides</div>
	      <img src='https://chart.googleapis.com/chart?cht=qr&chl=http%3A%2F%2Fslides.klipse.tech%2Fasciidoc%2Ffunctional-oop-clojure.html&chs=300x300' alt='qr code'></img>
              <div>
		  <a href="https://tinyurl.com/fun-oop"> https://tinyurl.com/fun-oop </a>
              </div>
	      
	      </div>
              <div class="left" style="width:50%;margin-top:30px;">
		<div>
                  <div>
                    <img class="fullscreen" src="./images/ldn-clj.png" alt="" width="150">
                  </div> 
                  <div>
		  <div>ClojureX 2018</div>
		  <div>London - Dec 3, 2018</div>
                  </div>
		</div>
              </div>
            </div>
            <div class="clear centered">
	    <br><br><br>
            </div>
	    <br><br>
	    <div style="font-size: 2em;">Yehonathan Sharvit</div>
	    <div style="font-size: 2em;"> <a href="https://twitter.com/viebel">@viebel</a></div></p>
        </div>
      </section>
++++

== Agenda

. Definitions and Rhetoric
. Functional Object Oriented Programming with maps
. Functional Object Oriented Programming with records and protocols


== Who am I?
[.left]
--
- Yehonathan Sharvit https://twitter.com/viebel[@viebel], viebel@gmail.com, https://www.linkedin.com/in/viebel/[LinkedIn]
- A pragmatic theorist
- Author of https://github.com/viebel/klipse[Klipse]
- A Web consultant: Full-Stack, clojure, clojurescript, node.js, react.js
- A Blogger about Functional Programming at http://blog.klipse.tech/
- Third year in a row at ClojureX
- Writing a book with Manning "Get Programming with Clojure" - MEAP launch today
--

image::images/me.jpg[me]


== Definitions and Rhetoric

[chapter-intro]
--
[text-centered]#Part 1#
Definition and Rhetoric
--

== Definitions and Rhetoric - Oxymoron


[%step]
--
*Rhetoric*:
Rhetoric is the art of using language to convince.
--

[%step]
--
*Oxymoron*:

An oxymoron is a rhetorical device containing words that seem to contradict each other.
--
[%step]
--
Examples:
--
[%step]
- The owner of the gallery told me that this picture was an *original copy*
- Do you like *Virtual Reality*?
- "Javascript is an awful language" is a *true myth*
- Yehonathan Sharvit is a *pragmatic theorist*


[%step]
--
Purpose:

- Create a little bit of *drama* for the reader.
- Make a person stop and think, whether that's to *laugh* or to *wonder*.
--

[%step]
But it doesn't always work, because...

== Definitions and Rhetoric - Oxymoron

image::images/stone.jpg[stone]

== Definitions and Rhetoric - OOP vs. FP

[%step]
--
*Object Oriented Programming*:
A programming paradigm based on the concept of *objects*, which  contain:

- *data*, in the form of fields
- *code*, in the form of procedures, often known as methods.

Methods can access and [yellow-background]#often modify the data fields of the object# with which they are associated.
--


[%step]
--
++++
<br>
<br>
++++

*Functional Programming*:
A programming paradigm based on the concept of *functions*
A *function*:

- receives *data* and returns *data*
- [yellow-background]#avoids mutating *data*#

--


== Definitons and Rhetoric - Summary

[quote]
Is "functional object oriented programming" an oxymoron?


== Definitions and Rhetoric - OOP redefined

[%step]
--
*Object Oriented Programming*:
A programming paradigm based on the concepts of *objects* and *messages*.

An *object* includes:

- a package of data
- a description of the operations that can be performed on that data

A *message* specifies one of the operations, but unlike a procedure, does not describe how the operation should be carried out.
--




[%step]
--
What if sending a message was done through a function?
--

== Definitions and Rhetoric - Functional OOP

[%step]
--
*Functional Object Oriented Programming*:
A programming paradigm based on the concepts of *functions*, *objects* and *messages*.


A *function*:

* receives *data* and returns *data*
* avoids mutating *data*

An *object* includes:

* a package of *data*
* a description of the operations that can be performed on that *data*
* [yellow-background]#operations avoid mutating data#: queries return *data* while commands return a *new version* of the object

A *message* specifies one of the operations without describing how the operation should be carried out.
Messages are sent through a function.
--

== Functional OOP with maps
[chapter-intro]
--
Part 2
Functional OOP with maps
--


== Functional OOP with maps - human's essence

[%step]
--
What is the essence of a human?
--

[%step]
. A human has a *name*
. A human has an *appellation* - how other humans refer to him
. A human can *drink*
. When a human drinks too much, he is *drunk*


[%step]
--
image::images/drunk.png[mutation, width=1000]
--


== Functional OOP with maps

A "constructor" is a function that returns a map with *data* and *operations*
[%step]
--
++++
<br>
++++
[source.clj]
----
(defn new-human-0 [name]
  {:data {:name name
          :drinks 0}
   :ops {:appellation (fn [this] "(:appellation will be implemented soon")
         :take-some-drinks (fn [this how-many] (str "(:take-some-drinks " how-many ") will be implemented soon"))
         :drunk? (fn [this] "(:drunk? will be implemented soon")}})
----
--

[%step]
--
How does a human look like? 
[source.clj]
----
(new-human-0 "Adam")
----
--


[%step]
--
*Data* inspection is trivial
[source.clj]
----
(:data (new-human-0 "Adam"))
----
--

[%step]
--
To send a *message*, we retrieve the function from the *:ops* map and call it with the _object_ and the _args_.
[source.clj]
----
(defn send-msg [obj op-name & args]
  (let [op (get-in obj [:ops op-name])]
      (apply op obj args)))
----
--

[%step]
--
Let's ask Adam to take a drink
--

[%step]
--
[source.clj]
----
(send-msg (new-human-0 "Adam") :take-some-drinks 1)
----
--


== Functional OOP with maps - Queries

[%step]
--

Methods that query data are easy to implement

[source.clj]
----
(defn new-human-1 [name]
  {:data {:name name
          :drinks 0}
   :ops {:appellation (fn [this] (get-in this [:data :name]))
         :take-some-drinks (fn [this how-many] (str "(:take-some-drinks " how-many ") will be implemented soon"))
         :drunk? (fn [this] (> (get-in this [:data :drinks]) 3))}})
----
--

[%step]
--
Let's send a *:appellation* message to a human
[source.clj]
----
(send-msg (new-human-1 "Adam") :appellation)
----
--


[%step]
--
++++
<br>
<br>
++++
[meditation]#Why do we need both *:appellation* operation and *:name* field?#
--

[%step]
--
[meditation]#Why do we need to store the *name* in the *:data* instead of accessing the lexical scope?#
--

== Functional OOP with maps - commands

[%step]
--
What about mutations? 
++++
<br>
<br>
++++
--

[%step]
--
image::images/mutation_is_bad.jpg[mutation, width=1000]
--

== Functional OOP with maps - commands first attempt

[%step]
--
Clojure is powerful: let's use atoms and *swap!* them
[source.clj]
----
(let [my-counter (atom 0)]
  (dotimes [_ 10] (swap! my-counter inc))
  @my-counter)
----
--

[%step]
--
Let's make the *:drinks* data field an atom
[source.clj]
----
(defn new-human-with-atoms [name]
  {:data {:name name
          :drinks (atom 0)}
   :ops {:appellation (fn [this] (get-in this [:data :name]))
         :take-some-drinks (fn [this how-many]
                             (swap! (get-in this [:data :drinks]) (partial + how-many))
                             this)
         :drunk? (fn [this] (> @(get-in this [:data :drinks]) 3))}})
----
--

[%step]
--
++++
<br>
<br>
++++
Let's ask Noah to take a couple of drinks

[source.clj]
----
(let [noah (new-human-with-atoms "Noah")]
  #_(send-msg noah :take-some-drinks 1)
    #_(send-msg noah :take-some-drinks 1)
      #_(send-msg noah :take-some-drinks 1)
        #_(send-msg noah :take-some-drinks 1)
	  (send-msg noah :drunk?))
----
--
++++
<br>
<br>
++++
[%step]
--
That's not what we are looking for: Now, we have different states for *Noah*.

The code is harder to _reason about_.
--


[%step]
--
Remember: After a drink, a human is not the same human

image::images/drunk.png[mutation, width=1000]
--



== Functional OOP with maps - commands without mutations

[%step]
--
Clojure is famous for its immutable data structures.
Let's leverage them!
--
[%step]
--
Let's use *update-in*
[source.clj]
----
(update-in {:a {:b 0}} [:a :b] (partial + 3))
----
++++
<br>
++++
--

[%step]
--
[source.clj]
----
(defn new-human [name]
  {:data {:name name
          :drinks 0}
   :ops {:appellation (fn [this] (get-in this [:data :name]))
         :drunk? (fn [this] (> (get-in this [:data :drinks]) 3))
         :take-some-drinks (fn [this how-many]
                             (update-in this
                                        [:data :drinks]
                                        (partial + how-many)))}})
----
--

[%step]
--
++++
<br>
++++
Let's create an *object* and "mutate" it:

[source.clj]
----
(let [abraham (new-human "Abraham")]
  #_(send-msg abraham :take-some-drinks 1)
    #_(send-msg abraham :take-some-drinks 1)
      #_(send-msg abraham :take-some-drinks 1)
        #_(send-msg abraham :take-some-drinks 1)
	    (send-msg abraham :drunk?))
----
++++
<br>
++++
Abraham looks much more resistant to alcohol than Noah!
++++
--

[%step]
--
++++
<br>
++++
Let's explore the data 

[source.clj]
----
(let [abraham (new-human "Abraham")]
  (send-msg abraham :take-some-drinks 1)
    (send-msg abraham :take-some-drinks 1)
      (send-msg abraham :take-some-drinks 1)
        (send-msg abraham :take-some-drinks 1)
	  (:data abraham))
----
--

[%step]
--
When we manipulate a map in Clojure, we create new versions of the map.
[source.clj]
----
(let [abraham (new-human "Abraham")
      abraham-1 (send-msg abraham :take-some-drinks 1)
            abraham-2 (send-msg abraham-1 :take-some-drinks 1)
	          abraham-3 (send-msg abraham-2 :take-some-drinks 1)
		        abraham-4 (send-msg abraham-3 :take-some-drinks 1)]
			  (send-msg abraham-4 :drunk?))
----
--

== Functional OOP with maps - commands with thread macro


[%step]
--
Thread macro
[source.clj]
----
(-> {}
    (assoc :a 1)
    (assoc :b 2))
----
--

[%step]
--
Let's rewrite this piece of code
[source.clj]
----
(let [abraham (new-human "Abraham")
      abraham-1 (send-msg abraham :take-some-drinks 2)
      abraham-2 (send-msg abraham-1 :take-some-drinks 3)]
  (send-msg abraham-2 :drunk?))
----
--

[%step]
--
The code looks shorter with a thread macro
[source.clj]
----
(-> (new-human "Abraham")
    (send-msg :take-some-drinks 2)
    (send-msg :take-some-drinks 3)
    (send-msg :drunk?))
----
--


== Functional OOP with maps - Polymorphism

[%step]
--
Let's create another object that implements the same operations but behaves differently...
--
[%step]
* A king's appellation contains a title
* A king is drunk after 5 drinks

[%step]
--
[source.clj]
----
(defn new-king [name]
  {:data {:name name
          :drinks 0}
   :ops {:appellation (fn [this] (str "King " (get-in this [:data :name])))
         :drunk? (fn [this] (> (get-in this [:data :drinks]) 5))
         :take-some-drinks (fn [this how-many]
                             (update-in this
                                        [:data :drinks]
                                        (partial + how-many)))}})
----
--

[%step]
--
When we send a message, we don't know what procedure is going to be called
--

[%step]
--
David, the king
[source.clj]
----
(let [david (new-king "David")] 
  (send-msg david :appellation))
----

David, the human
[source.clj]
----
(let [david (new-human "David")] 
  (send-msg david :appellation))
----
Both pieces of code that send the message look the same, but the behaviour is different.
--


== Functional OOP with maps - summary

[%step]
--
We have created our own Object Oriented "system"
--

[%step]
--
[source.clj]
----
(defn new-human [name]
  {:data {:name name
          :drinks 0}
   :ops {:appellation (fn [this] (get-in this [:data :name]))
         :drunk? (fn [this] (> (get-in this [:data :drinks]) 3))
         :take-some-drinks (fn [this how-many]
                             (update-in this
                                        [:data :drinks]
                                        (partial + how-many)))}})

(defn send-msg [obj op-name & args]
  (let [op (get-in obj [:ops op-name])]
      (apply op obj args)))
----
--

[%step]
--
Nothing unique to Clojure.
But Clojure was really helpful:

. Immutable data collections
. Thread macros
--

[%step]
--
But it's not really a system:

. Custom *:data* and *:ops* structure
. Custom *send-msg* function
. We have messages but we don't have interfaces
. Methods access data trough *this* instead of lexical scope
--


== Functional OOP with Records and Protocols
[chapter-intro]
--
Part 3
Functional OOP with Records and Protocols
--

== Functional OOP with Records and Protocols

In Clojure there are three ways to create an object (it's called a datatype):

. deftype (low level)
. defrecord (application level)
. reify (anonymous)

We will focus on *defrecord*


== Functional OOP with Records and Protocols - The 5 commandments 

[bible]
--
[%step]
. Thou Shalt Not Encapsulate Information (Transparency)
. Thou Shalt Not Mutate an Object (Immutability)
. Thou Shalt Only Program to an Interface (Polymorphism)
. Thou Shalt Not Derive from a Concrete Class (Simplicity)
. Let Thy Friend Extend a Class as Thyself (Extensibility)
--



== Functional OOP with Records and Protocols

First, we define a protocol:

[%step]
--
[source.clj]
----
(defprotocol IHuman
  (appelation [this])
  (drunk? [this])
  (take-some-drinks [this how-many]))
----
--

[%step]
--
Then, a record that implements the protocol:
[source.clj]
----
(defrecord Human [name drinks]
  IHuman
  (appelation [this] name)
  (drunk? [this] (> drinks 3))
  (take-some-drinks [this how-many]
                    (update this
                               :drinks
                               (partial + how-many))))
----
--

[%step]
--
And a custom constructor 
[source.clj]
----
(defn create-human [name]
  (Human. name 0))
----
--


[%step]
--
Let's create a human and ask him to take some drinks
[source.clj]
----
(let [abraham (create-human "Abraham")]
  (-> abraham
      (take-some-drinks 11)
      drunk?))
----
--

[%step]
--
Beware of this common pitfall
[source.clj]
----
(let [adam (create-human "Adam")]
  (take-some-drinks adam 10)
  (drunk? adam))
----
--

== Functional OOP with Records - constructors

[%step]
--
++++
<br>

++++
[meditation]#Why do we need a custom constructor?#
--

[%step]
--
image::images/indirection.jpg[mutation, width=1000]
--


== Records and Protocols - Transparency

[%step]
--
[bible]#1. Thou Shalt Not Encapsulate Information (Transparency)#
--
[%step]
--
[source.clj]
----
(create-human "Adam")
----
--


[%step]
--
[source.clj]
----
(take-some-drinks (create-human "Adam") 10)
----
--

== Records and Protocols - Immutability

[%step]
[bible]#2. Thou Shalt Not Mutate an Object (Immutability)#

[%step]
--
[source.clj]
----
(let [adam (create-human "Adam")]
  (take-some-drinks adam 10)
  adam)
----
--

[%step]
--
Successive operations usually done with the thread macro
[source.clj]
----
(let [abraham (create-human "Abraham")]
  (-> abraham
      (take-some-drinks 2)
      (take-some-drinks 3)))
----
--

[%step]
--
Remember: After a drink, a human is not the same human

image::images/drunk.png[mutation, width=1000]
--


== Records and Protocols - Strict Polymorphism


[%step]
[bible]#3. Thou Shalt Only Program to an Interface (Strict Polymorphism)#

[%step]
--
The syntax doesn't allow adding a method to a record without specifying a protocol
--
[%step]
--
[source.clj]
----
(defrecord IAmAlone []
  (foo [this] (str "sniff...")))
----
--


[%step]
--
If we add a method that is not part of the protocol
[source.clj]
----
(defrecord IAmAlone []
IHuman
  (foo [this] (str "sniff...")))
----
--

[%step]
--
Bad things happen
[source.clj]
----
(foo (IAmAlone.))
----
--

== Records and protocols - Simplicity

[%step]
--
[bible]#4. Thou Shalt Not Derive from a Concrete Class (Simplicity)#
--

[%step]
--
Concrete derivation is bad!

Clojure blocks it. You can try composition
--

== Records and Protocols - Extensibility

[%step]
[bible]#5. Let Thy Friend Extend a Class as Thyself (Extensibility)#

[%step]
--
In order to add new functionality to a record, we have to create a new protocol:
[source.clj]
----
(defprotocol IStatus
  (status [this]))
----
--

[%step]
--
Although the *Human* record is already defined, we can still extend it.
A record is never closed!
--

[%step]
--
The original *Human* record doesn't have a status
[source.clj]
----
(def adam (create-human "Adam"))
(status adam)
----

--
[%step]
--
Let's extend the *Human* record to the *IStatus* protocol 
[source.clj]
----
#_(extend-type Human
  IStatus
  (status [this] (str (:name this) " has drunk " (:drinks this) " drinks")))
----
--

[%step]
--
Now, Adam has a status:
[source.clj]
----
#_(status adam)
----
--


== Functional OOP in Clojure - Records are wacky maps

Records look like maps

[%step]
--
[source.clj]
----
(println (Human. "Adam" 5))
----
--

[%step]
--
++++
<br>
++++
They have keys

[source.clj]
----
(keys (Human. "Adam" 5))
----
--

[%step]
--
++++
<br>
++++
keys are functions

[source.clj]
----
(:name (Human. "Adam" 5))
----
--

[%step]
--
++++
<br>
++++

[red]#But records are not functions#

[source.clj]
----
((Human. "Adam" 5) :name)
----
--

[%step]
--
++++
<br>
++++
You can assoc them new keys

[source.clj]
----
(println (assoc (Human. "Adam" 5) :books 88))
----
--

[%step]
--
++++
<br>
++++
[red]#But be careful when you dissoc them#

[source.clj]
----
(type (dissoc (Human. "Adam" 5) :name))
----
++++
<br>
<br>
++++
--


== One last thing

[%step]
--
In Clojure, everything can behave like a function
--

[%step]
--
[source.clj]
----
#_(extend-type Human
  IFn
  (-invoke
    ([this key]
     (get this key))))
----
--

[%step]
--
[source.clj]
----
#_((create-human "Adam") :name)
----
++++
<br>
<br>
++++
--

[%step]

Funny thought: In Clojure, even functions are protocols.

== Conclusion

What is the essence of Clojure?

[%step]

- LISP syntax ?
- Functional programming ?
- Immutability ?


== Questions

++++
<div class="left" style="width:50%; margin-top: 300px;">
   <div class="centered">
   <p>
   Early access version of the book
   </p>
      <img src='https://chart.googleapis.com/chart?cht=qr&chl=https%3A%2F%2Ftinyurl.com%2Fget-clojure&chs=300x300' alt='qr code'/>
   <p>
     <a href="https://tinyurl.com/get-clojure"> https://tinyurl.com/get-clojure </a>
   </p>
     <p>DISCOUNT CODE: <span style="font-weight: bold;">mlsharvit</span> (50% off)</p>
     <p>Valid until December 9, 2018</p>
   </div>
</div>
++++

++++
<div class="left" style="width:50%;">
   <img style="width: 80%" src='./images/get-programming-clojure-book.png'/>
</div>
++++


++++
<style>
.bible {
font-family: cursive;
font-size: 2.5em;
}
.chapter-intro {
font-size: 2.5em;
font-weight: bold;
color: #63b132;
margin-top: 300px;
text-align: center;
}
.yellow-background {
background: yellow;
}
.red {
color: red;
}
</style>
++++

++++
<style>
.CodeMirror {
    border: solid 2px #90B4FE !important;
    width: 80%;
    left: 10%;
    font-size: 24px;
}

.klipse-container {
    margin-bottom: 10px;
    left: 12%;
    position: absolute;
}

.vertical-padder {
height: 40vh;
} 
</style>
++++

include::klipse.adoc[]